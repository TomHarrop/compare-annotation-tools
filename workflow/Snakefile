#!/usr/bin/env python3

from pathlib import Path
from snakemake.logging import logger


# tool config (different to run config)
configfile: "config/tools.yaml"


genomes_dict = config["genomes"]
tools_dict = config["tools"]
utils = config["utils"]

all_genomes = sorted(set(genomes_dict.keys()))
all_tools = sorted(set(tools_dict.keys()))


wildcard_constraints:
    genome="|".join(all_genomes),
    tool="|".join(all_tools),


# order is important, e.g. utils has functions needed by braker3
include: "rules/utils.smk"
include: "rules/collect_rnaseq_data.smk"
include: "rules/braker3.smk"


rule generate_target_file:
    input:
        get_tool_result_files,
    output:
        outdir=directory(Path("results", "{genome}", "{tool}")),
    params:
        directories=lambda wildcards, input: sorted(
            set(subpath(x, parent=True) for x in input)
        ),
        outdir=subpath(output.outdir, parent=True),
    shell:
        "cp -r {params.directories} {params.outdir}/"


rule target:
    default_target: True
    input:
        expand(
            Path("results", "{genome}", "{tool}"), genome=all_genomes, tool=all_tools
        ),
